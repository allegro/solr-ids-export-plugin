buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.12.0'
    }
}

plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id 'com.bmuschko.nexus' version '2.3.1'
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = project.rootProject.name
    }
    versionCreator 'versionWithBranch'
}

sourceCompatibility = 1.8

project.group = 'pl.allegro.search.solr'
project.version = scmVersion.version

repositories {
    mavenCentral()
    maven { url "https://maven.restlet.com" }
}

dependencies {
    compile group: 'org.apache.solr', name: 'solr-core', version: '7.2.1'
    compile group: 'org.jetbrains', name:'annotations', version: '13.0'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    testCompile group: 'org.apache.solr', name: 'solr-test-framework', version: "7.2.1"
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '0.9.24'
}

compileJava {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs << '-parameters'
}


test {
    testLogging {
        exceptionFormat = 'full'
    }
    systemProperty "test.solr.allowed.securerandom", "NativePRNG"
}

wrapper {
    gradleVersion = '5.6.2'
}

modifyPom {
    project {
        name project.name
        description 'Set of Solr plugins allowing time-efficient export of Ids of all found documents.'
        url 'https://github.com/allegro/solr-ids-export-plugin'
        inceptionYear '2020'

        scm {
            url 'https://github.com/allegro/solr-ids-export-plugin'
            connection 'scm:git@github.com:allegro/solr-ids-export-plugin.git'
            developerConnection 'scm:git@github.com:allegro/solr-ids-export-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'lepton'
                name 'Team Lepton'
            }
        }
    }
}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}
